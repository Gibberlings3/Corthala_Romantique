//Set Matches

/* game that carries over from SoA */
IF
Global("H#CheckValygarMatchTOB","GLOBAL",0)
InParty(Myself)
Global("H#ValygarMatch","GLOBAL",1)
Global("H#ValygarRomanceActive","GLOBAL",2)
Global("P#VaLoveTalk","GLOBAL",0)
THEN
RESPONSE #100
SetGlobal("ValygarRomanceActive","GLOBAL",2)
SetGlobal("H#CheckValygarMatchTOB","GLOBAL",2)
RealSetGlobalTimer("P#VaLoveTalkTime","GLOBAL",180)
RealSetGlobalTimer("P#VaForestTalkTime","GLOBAL",%ValygarToBTimer_short%)
SetGlobal("P#VaLoveTalk","GLOBAL",1)
END

/* deactivate the "ValygarRomanceActive" in case romance is broken! */
IF
    Global("ValygarRomanceActive","GLOBAL",2)
    Global("H#ValygarRomanceActive","GLOBAL",3)
THEN
    RESPONSE #100
       SetGlobal("ValygarRomanceActive","GLOBAL",0)
END

/* In case of serious romance, deactivate the others */
IF
    InParty(Myself)
    CombatCounter(0)
    !See([ENEMY])
    Global("H#ValygarRomanceActive","GLOBAL",2)
    Global("AnomenRomanceActive","GLOBAL",1)
    Global("H#DeactivateRomAnomen","LOCALS",0)
THEN
    RESPONSE #100
        SetGlobal("AnomenRomanceActive","GLOBAL",3)
	SetGlobal("H#DeactivateRomAnomen","LOCALS",1)
END

IF
    InParty(Myself)
    CombatCounter(0)
    !See([ENEMY])
    Global("H#ValygarRomanceActive","GLOBAL",2)
    Global("RasaadRomanceActive","GLOBAL",1)
    Global("H#DeactivateRomRasaad","LOCALS",0)
THEN
    RESPONSE #100
        SetGlobal("RasaadRomanceActive","GLOBAL",3)
	SetGlobal("H#DeactivateRomRasaad","LOCALS",1)
END

IF
    InParty(Myself)
    CombatCounter(0)
    !See([ENEMY])
    Global("H#ValygarRomanceActive","GLOBAL",2)
    Global("DornRomanceActive","GLOBAL",1)
    Global("H#DeactivateRomDorn","LOCALS",0)
THEN
    RESPONSE #100
        SetGlobal("DornRomanceActive","GLOBAL",3)
	SetGlobal("H#DeactivateRomDorn","LOCALS",1)
END

IF
    InParty(Myself)
    CombatCounter(0)
    !See([ENEMY])
    Global("H#ValygarRomanceActive","GLOBAL",2)
    Global("HexxatRomanceActive","GLOBAL",1)
    Global("H#DeactivateRomHexxat","LOCALS",0)
THEN
    RESPONSE #100
        SetGlobal("HexxatRomanceActive","GLOBAL",3)
	SetGlobal("H#DeactivateRomHexxat","LOCALS",1)
END



//No Romance if there is a serious one going on 
IF
    InParty(Myself)
    InParty("Anomen")
    Global("AnomenRomanceActive","GLOBAL",2)
    !Global("H#ValygarRomanceActive","GLOBAL",3)
THEN
    RESPONSE #100
       SetGlobal("H#ValygarRomanceActive","GLOBAL",3)
END

IF
    InParty(Myself)
    InParty("Rasaad")
    Global("RasaadRomanceActive","GLOBAL",2)
    !Global("H#ValygarRomanceActive","GLOBAL",3)
THEN
    RESPONSE #100
       SetGlobal("H#ValygarRomanceActive","GLOBAL",3)
END

IF
    InParty(Myself)
    InParty("Dorn")
    Global("DornRomanceActive","GLOBAL",2)
    !Global("H#ValygarRomanceActive","GLOBAL",3)
THEN
    RESPONSE #100
       SetGlobal("H#ValygarRomanceActive","GLOBAL",3)
END

IF
    InParty(Myself)
    InParty("Hexxat")
    Global("HexxatRomanceActive","GLOBAL",2)
    !Global("H#ValygarRomanceActive","GLOBAL",3)
THEN
    RESPONSE #100
       SetGlobal("H#ValygarRomanceActive","GLOBAL",3)
END


/* new ToB game - start player1 dialogue to chose */
IF
InParty(Myself)
Global("H#CheckValygarMatchTOB","GLOBAL",0)
Global("H#ValygarMatch","GLOBAL",0)
Global("H#ValygarRomanceActive","GLOBAL",0)
Global("P#VaLoveTalk","GLOBAL",0)
Gender(Player1,FEMALE)
	OR(4)
                    Race(Player1,HUMAN)
                    Race(Player1,HALF_ELF)
                    Race(Player1,ELF)
                    Race(Player1,HALFORC)
	OR(4)
		Alignment(Player1,MASK_GOOD)
		Alignment(Player1,LAWFUL_NEUTRAL)
		Alignment(Player1,NEUTRAL)
		Class(Player1,DRUID_ALL)
THEN
RESPONSE #100
SetGlobal("H#CheckValygarMatchTOB","GLOBAL",1)
END

/* initiation */
IF
    InParty(Myself)
    See(Player1)
    !StateCheck(Myself,CD_STATE_NOTVALID)
    !StateCheck(Player1,CD_STATE_NOTVALID)
    CombatCounter(0)
    !See([ENEMY])
    Global("H#CheckValygarMatchTOB","GLOBAL",1)
THEN
    RESPONSE #100
	ActionOverride(Player1,StartDialog("player1",Player1))
END

/* first lovetalk - needs to run first */

IF
    InParty(Myself)
    See(Player1)
    !StateCheck(Myself,CD_STATE_NOTVALID)
    !StateCheck(Player1,CD_STATE_NOTVALID)
    CombatCounter(0)
    !See([ENEMY])
RealGlobalTimerExpired("P#VaLoveTalkTime","GLOBAL")
ReputationGT(Player1,9)
Global("H#ValygarMatch","GLOBAL",1)
Global("H#ValygarRomanceActive","GLOBAL",2)
Global("P#VaLoveTalk","GLOBAL",1)
THEN
RESPONSE #100
RealSetGlobalTimer("P#VaLoveTalkTime","GLOBAL",%ValygarToBTimer_short%) 
IncrementGlobal("P#VaLoveTalk","GLOBAL",1)
END


/* morning after first love night */
IF
    Global("P#ToBMorning1","GLOBAL",1)
    InParty(Myself)
    See(Player1)
    !StateCheck(Myself,CD_STATE_NOTVALID)
    !StateCheck(Player1,CD_STATE_NOTVALID)
    CombatCounter(0)
    !See([ENEMY])
Global("H#ValygarMatch","GLOBAL",1)
Global("H#ValygarRomanceActive","GLOBAL",2)
THEN
RESPONSE #100
SetGlobal("P#ToBMorning1","GLOBAL",2)
END

/* initiation */
IF
    InParty(Myself)
    See(Player1)
    !StateCheck(Myself,CD_STATE_NOTVALID)
    !StateCheck(Player1,CD_STATE_NOTVALID)
    CombatCounter(0)
    !See([ENEMY])
    Global("P#ToBMorning1","GLOBAL",2)
THEN
    RESPONSE #100
        //PlaySong(999999)
StartDialogueNoSet(Player1)
END

// Talk at the pocket Plane
/* activation */
IF
Global("H#ValygarRomanceActive","GLOBAL",2)
    InParty(Myself)
    See(Player1)
    !StateCheck(Myself,CD_STATE_NOTVALID)
    !StateCheck(Player1,CD_STATE_NOTVALID)
    CombatCounter(0)
    !See([ENEMY])
Global("P#VaLoveTalk","GLOBAL",9)
AreaCheck("AR4500")
THEN
RESPONSE #100
RealSetGlobalTimer("P#VaLoveTalkTime","GLOBAL",%ValygarToBTimer_short%) 
IncrementGlobal("P#VaLoveTalk","GLOBAL",1)
END


//Floaters

//Yaga-Sura's Mother Dead
/* activation */
IF
    InParty(Myself)
    See(Player1)
    !StateCheck(Myself,CD_STATE_NOTVALID)
    !StateCheck(Player1,CD_STATE_NOTVALID)
    CombatCounter(0)
    !See([ENEMY])
Dead("hgnya01")
RealGlobalTimerExpired("P#VaLoveTalkTime","GLOBAL")
ReputationGT(Player1,9)
Global("H#ValygarMatch","GLOBAL",1)
Global("H#ValygarRomanceActive","GLOBAL",2)
Global("P#VaFreeTalk1","GLOBAL",0)
THEN
RESPONSE #100
RealSetGlobalTimer("P#VaLoveTalkTime","GLOBAL",%ValygarToBTimer_short%) 
SetGlobal("P#VaFreeTalk1","GLOBAL",2)
END

/* initiation */
IF
    InParty(Myself)
    See(Player1)
    !StateCheck(Myself,CD_STATE_NOTVALID)
    !StateCheck(Player1,CD_STATE_NOTVALID)
    CombatCounter(0)
    !See([ENEMY])
    Global("P#VaFreeTalk1","GLOBAL",2)
THEN
    RESPONSE #100
        //PlaySong(999999)
StartDialogueNoSet(Player1)
END



// Strategy Talk
/* activation */
IF 
    InParty(Myself)
    See(Player1)
    !StateCheck(Myself,CD_STATE_NOTVALID)
    !StateCheck(Player1,CD_STATE_NOTVALID)
    CombatCounter(0)
    !See([ENEMY])
Global("EnteredArea5203","GLOBAL",1) //after Saradush fall
RealGlobalTimerExpired("P#VaLoveTalkTime","GLOBAL")
ReputationGT(Player1,9)
Global("H#ValygarMatch","GLOBAL",1)
Global("H#ValygarRomanceActive","GLOBAL",2)
GlobalGT("P#VaFreeTalk2","GLOBAL",0) //Saradush dialogue run
Global("P#VaFreeTalk4","GLOBAL",0)
THEN
RESPONSE #100
RealSetGlobalTimer("P#VaLoveTalkTime","GLOBAL",%ValygarToBTimer_short%) 
SetGlobal("P#VaFreeTalk4","GLOBAL",2)
END

/* initiation */
IF
    InParty(Myself)
    See(Player1)
    !StateCheck(Myself,CD_STATE_NOTVALID)
    !StateCheck(Player1,CD_STATE_NOTVALID)
    CombatCounter(0)
    !See([ENEMY])
    Global("P#VaFreeTalk4","GLOBAL",2)
THEN
    RESPONSE #100
        //PlaySong(999999)
RealSetGlobalTimer("P#VaLoveTalkTime","GLOBAL",%ValygarToBTimer_short%) 
StartDialogueNoSet(Player1)
END


//Dead - Abazigal
/* activation */
IF
RealGlobalTimerExpired("P#VaLoveTalkTime","GLOBAL")
Global("H#ValygarRomanceActive","GLOBAL",2)
    InParty(Myself)
    See(Player1)
    !StateCheck(Myself,CD_STATE_NOTVALID)
    !StateCheck(Player1,CD_STATE_NOTVALID)
    CombatCounter(0)
    !See([ENEMY])
Dead("Abazigal1")
Global("P#VaFreeTalk5","GLOBAL",0)
THEN
RESPONSE #100
RealSetGlobalTimer("P#VaLoveTalkTime","GLOBAL",%ValygarToBTimer_short%) 
SetGlobal("P#VaFreeTalk5","GLOBAL",2)
END

/* initiation */
IF
    InParty(Myself)
    See(Player1)
    !StateCheck(Myself,CD_STATE_NOTVALID)
    !StateCheck(Player1,CD_STATE_NOTVALID)
    CombatCounter(0)
    !See([ENEMY])
    Global("P#VaFreeTalk5","GLOBAL",2)
THEN
    RESPONSE #100
        //PlaySong(999999)
RealSetGlobalTimer("P#VaLoveTalkTime","GLOBAL",%ValygarToBTimer_short%) 
StartDialogueNoSet(Player1)
END




//Sequence itself


IF
    InParty(Myself)
    See(Player1)
    !StateCheck(Myself,CD_STATE_NOTVALID)
    !StateCheck(Player1,CD_STATE_NOTVALID)
    CombatCounter(0)
    !See([ENEMY])
RealGlobalTimerExpired("P#VaLoveTalkTime","GLOBAL")
ReputationGT(Player1,9)
Global("H#ValygarMatch","GLOBAL",1)
Global("H#ValygarRomanceActive","GLOBAL",2)
OR(7)
//Global("P#VaLoveTalk","GLOBAL",1) above, triggered as first LT
Global("P#VaLoveTalk","GLOBAL",3)
Global("P#VaLoveTalk","GLOBAL",5)
Global("P#VaLoveTalk","GLOBAL",7)
//Global("P#VaLoveTalk","GLOBAL",9) above, in PP
Global("P#VaLoveTalk","GLOBAL",11)
Global("P#VaLoveTalk","GLOBAL",13)
Global("P#VaLoveTalk","GLOBAL",15)
Global("P#VaLoveTalk","GLOBAL",17)
THEN
RESPONSE #100
IncrementGlobal("P#VaLoveTalk","GLOBAL",1)
END

IF 
    InParty(Myself)
    See(Player1)
    !StateCheck(Myself,CD_STATE_NOTVALID)
    !StateCheck(Player1,CD_STATE_NOTVALID)
    CombatCounter(0)
    !See([ENEMY])
OR(9)
Global("P#VaLoveTalk","GLOBAL",2)
Global("P#VaLoveTalk","GLOBAL",4)
Global("P#VaLoveTalk","GLOBAL",6)
Global("P#VaLoveTalk","GLOBAL",8)
Global("P#VaLoveTalk","GLOBAL",10)
Global("P#VaLoveTalk","GLOBAL",12) 
Global("P#VaLoveTalk","GLOBAL",14)
Global("P#VaLoveTalk","GLOBAL",16)
Global("P#VaLoveTalk","GLOBAL",18)
THEN
RESPONSE #100
RealSetGlobalTimer("P#VaLoveTalkTime","GLOBAL",%ValygarToBTimer_short%)
        //PlaySong(999999)
        StartDialogNoSet(Player1)
END



//Morning After Saradush rest dialogue

/* initiation */
IF
    InParty(Myself)
    See(Player1)
    !StateCheck(Myself,CD_STATE_NOTVALID)
    !StateCheck(Player1,CD_STATE_NOTVALID)
    CombatCounter(0)
    !See([ENEMY])
    Global("P#VaFreeTalk2","GLOBAL",4)
THEN
    RESPONSE #100
        //PlaySong(999999)
StartDialogueNoSet(Player1)
END

// Valygar Collects Flowers in the morning

/* Global("P#VaFreeTalk3","GLOBAL",1) set by P#VAROD.baf */
IF 
    Global("P#VaFreeTalk3","GLOBAL",1)
    InParty(Myself)
    See(Player1)
    !StateCheck(Myself,CD_STATE_NOTVALID)
    !StateCheck(Player1,CD_STATE_NOTVALID)
    CombatCounter(0)
    !See([ENEMY])
THEN
RESPONSE #100
//PlaySong(999999)
StartDialogueNoSet(Player1)
END




// Two Talks in the forest 

IF
Global("H#ValygarRomanceActive","GLOBAL",2)
    InParty(Myself)
    See(Player1)
    !StateCheck(Myself,CD_STATE_NOTVALID)
    !StateCheck(Player1,CD_STATE_NOTVALID)
    CombatCounter(0)
    !See([ENEMY])
GlobalGT("P#VaLoveTalk","GLOBAL",2)
RealGlobalTimerExpired("P#VaForestTalkTime","GLOBAL")
ReputationGT(Player1,9)
OR(2)
	Global("P#VaForestTalk","GLOBAL",0)
	Global("P#VaForestTalk","GLOBAL",2)
OR(2)
	AreaType(FOREST)
	AreaCheck("AR6100")
!AreaType(CITY)
THEN
RESPONSE #100
IncrementGlobal("P#VaForestTalk","GLOBAL",1)
END

IF 
InParty(Myself)
    See(Player1)
    !StateCheck(Myself,CD_STATE_NOTVALID)
    !StateCheck(Player1,CD_STATE_NOTVALID)
    CombatCounter(0)
    !See([ENEMY])
OR(2)
	Global("P#VaForestTalk","GLOBAL",1)
	Global("P#VaForestTalk","GLOBAL",3)
THEN
RESPONSE #100
        //PlaySong(999999)
RealSetGlobalTimer("P#VaLoveTalkTime","GLOBAL",%ValygarToBTimer_short%)
RealSetGlobalTimer("P#VaForestTalkTime","GLOBAL",%ValygarToBTimer_long%)
StartDialogueNoSet(Player1)
END




